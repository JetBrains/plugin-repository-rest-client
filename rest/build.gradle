dependencies {
  implementation("com.squareup.retrofit2:retrofit:2.9.0") {
    exclude module: 'okhttp'
  }
  implementation("com.squareup.retrofit2:converter-jaxb:2.9.0")
  implementation("com.squareup.retrofit2:converter-jackson:2.9.0")
  implementation(libs.jackson.databind)
  implementation(libs.okhttp)
  implementation(libs.jackson.kotlin) {
    // otherwise `jackson-module-kotlin` would bring kotlin-reflect:1.4.21
    // TODO: check it, we are using a newer `jackson-module-kotlin` with kotlin-reflect:1.5.32
    exclude group: "org.jetbrains.kotlin", module: "kotlin-reflect"
  }
  implementation("com.sun.xml.bind:jaxb-impl:2.3.2")
  implementation("com.sun.xml.bind:jaxb-core:2.3.0")
  implementation("org.jetbrains.intellij:blockmap:1.0.5")

  testImplementation("junit:junit:4.13.2")
  testImplementation("org.assertj:assertj-core:3.12.2")
}

test {
  useJUnit()
  group = "Rest client tests"
  description = "Runs rest client tests"

  def baseUrl = System.getProperty("jetbrains.plugins.baseUrl", "https://plugins.jetbrains.com")
  def testToken = System.getProperty("jetbrains.plugins.testToken", null)
  def parallel = System.getProperty("parallel")?.toBoolean()
  def downloadPath = "${project.buildDir}/downloads/"
  mkdir(downloadPath)

  if (parallel) {
    maxParallelForks = Runtime.runtime.availableProcessors()
    logger.lifecycle("##teamcity[message text='Tests are running in parallel! (maxParallelForks = $maxParallelForks)' status='WARNING']")
  }

  systemProperties = [
    "jetbrains.plugin.download.path"  : downloadPath,
    "jetbrains.plugin.repository.host": baseUrl
  ]
  if (testToken != null) systemProperties.put("jetbrains.plugin.repository.token", testToken)

  finalizedBy "clearDownloadsDir"
}

task clearDownloadsDir(type: Delete) {
  delete "${project.buildDir}/downloads"
}
